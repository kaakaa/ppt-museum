group 'org.kaakaa'
version '0.0.1'

buildscript {
    repositories {
        maven { url "https://plugins.gradle.org/m2/" }
    }
    dependencies {
        classpath 'com.moowork.gradle:gradle-grunt-plugin:0.11'
        classpath 'com.moowork.gradle:gradle-node-plugin:0.11'
    }
}

apply plugin: 'java'
apply plugin: 'application'
apply plugin: 'com.moowork.grunt'
apply plugin: 'com.moowork.node'

mainClassName = 'org.kaakaa.pptmuseum.Main'

sourceCompatibility = 1.8
targetCompatibility = 1.8

repositories {
    mavenCentral()
    maven {
        url 'https://repository.apache.org/content/groups/snapshots/'
    }
}

dependencies {
    compile 'com.sparkjava:spark-core:2.3'
    compile 'com.sparkjava:spark-template-jade:2.3'
    compile 'org.mongodb:mongo-java-driver:2.14.1'
    compile 'org.mongodb.morphia:morphia:1.0.0-rc0'
    compile 'redis.clients:jedis:2.8.0'

    compile 'net.rakugakibox.util:yaml-resource-bundle:1.1'
    compile 'commons-fileupload:commons-fileupload:1.3.1'
    compile 'org.apache.httpcomponents:httpclient:4.5.1'
    compile 'org.apache.httpcomponents:httpmime:4.5.1'
    // compile 'org.apache.pdfbox:pdfbox:1.8.11'
    compile 'org.apache.pdfbox:pdfbox:2.0.0-SNAPSHOT'

    testCompile 'org.spockframework:spock-core:1.0-groovy-2.4'
}

jar {
    manifest {
        attributes 'Implementation-Title': 'ppt-museum', 'Implementation-Versio': version
        attributes 'Main-Class': mainClassName
    }
}

task startWeb(type:Exec) {
    commandLine "$buildDir/install/$project.name/bin/ppt-museum"
}

startWeb.dependsOn 'installDist'

// for installing node
node {
    version = '0.10.22'
    download = true
}

// grunt-browserify
grunt_build.dependsOn 'installGrunt'
grunt_build.dependsOn 'npmInstall'
processResources.dependsOn grunt_build
